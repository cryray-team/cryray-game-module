#include "common.h"
#include "sload.h"

// SSS Settings
#include "screenspace_addon\settings_screenspace_FLORA.h"

#ifdef	ATOC

float4 	main	( p_bumped I ) : SV_Target
{
	surface_bumped 	S 		= sload 	(I);
	S.base.w = (S.base.w-def_aref*0.5f)/(1-def_aref*0.5f);
	return S.base;
}

#else	//	ATOC

f_deffer 	main	( p_bumped I )
{
	f_deffer		O;

	surface_bumped 	S 		= sload 	(I);

	clip					(S.base.w-def_aref);

	// FLORA FIXES & IMPROVEMENTS - SSS Update 14
	// https://www.moddb.com/mods/stalker-anomaly/addons/screen-space-shaders/

	// Normal from texture and force Y vector to generate a nice fake SSS and specular
	// Sun works extremly well with this fake normal, but point lights and others need a fix later on 'accum_base.ps' and 'accum_omni_unshadowed.ps'
	float3 Ne	= mul(m_V, float3(S.normal.x, 1, S.normal.y));
	Ne = normalize(Ne);

	// Material value ( MAT_FLORA )
	float 	ms	= 0.15f;

	// Fake gloss
	S.gloss = gloss_params.x * saturate(S.base.g - (S.base.r + S.base.b) * 0.54f);
	// -----------------------------------------------------------------------

#ifdef USE_LM_HEMI
//	float4	lm 	= tex2D			(s_hemi, I.lmh);
	float4	lm 	= s_hemi.Sample( smp_rtlinear, I.lmh);
	//float 	h  	= dot			(lm.rgb,1.h/3.h);
	float 	h  	= get_hemi(lm);
# ifdef USE_R2_STATIC_SUN
//		 	ms 	= lm.w;
			ms 	= get_sun(lm);
# endif
#else
	float 	h	= I.position.w	;
# ifdef USE_R2_STATIC_SUN
		 	ms	= I.tcdh.w		;
# endif
#endif

	O = pack_gbuffer(
						float4	(Ne,										h),
  						float4 	(I.position.xyz + Ne*S.height*def_virtualh, ms),
  						float4	(S.base.rgb,								S.gloss) );

	return 	O	;
}

#endif //	ATOC
